# dependencies:
#    libglfw3-dev (3.2.1, ubuntu 18.04)
#    libmagick++-dev (8:6.9.7.4, ubuntu 18.04)
#    libassimp-dev (4.1.0, ubuntu 18.04)
#    libgles2-mesa-dev (18.0.5, ubuntu 18.04)
#    libglm-dev (0.9.9, ubuntu 18.04)
#    libboost-all-dev (1.65.1, ubuntu 18.04)


# opengle wrapper
AddOption('--use-glfw3', action='store_true', dest='use_glfw3', default=True,
	help='use GLFW3 opengl wrapper library')

AddOption('--use-glew', action='store_true', dest='use_glew', default=False,
	help='use GLEW opengl wrapper library')

# image manipulation
AddOption('--use-imagick', action='store_true', dest='use_imagick', default=True,
	help='use imagemagick to manipulate images')

AddOption('--use-gil', action='store_true', dest='use_gil', default=False,
	help='use boost::gil to manipulate images')


def create_build_environment():
	env = Environment(
		CCFLAGS=['--std=c++17', '-O0', '-g', '-Wall', '-Wno-sign-compare'],
		CPPPATH=['.', 'libs'],
		CPPDEFINES=['GLFW_INCLUDE_ES2', 'GLM_ENABLE_EXPERIMENTAL', 'GLM_FORCE_RADIANS'],
		LIBS=['boost_filesystem', 'boost_system']
	)

	env.ParseConfig('pkg-config --cflags --libs glesv2 assimp freetype2')

	# opengl wrapper
	if GetOption('use_glfw3'):
		env.Append(CPPDEFINES=['USE_GLFW3'])
		env.ParseConfig('pkg-config --cflags --libs glfw3')
	elif GetOption('use_glew'):
		env.Append(CPPDEFINES=['USE_GLEW'])
		env.ParseConfig('pkg-config --cflags --libs glew')

	# image manipulation
	if GetOption('use_imagick'):
		env.Append(CPPDEFINES=['USE_IMAGICK'])
		env.ParseConfig('pkg-config --cflags --libs ImageMagick++-im6.Q16')
	elif GetOption('use_gil'):
		env.Append(CPPDEFINES=['USE_GIL'])

	return env


env = create_build_environment()

program_objs = env.Object(['gles2/program_gles2.cpp'])

gles2_objs = [
	program_objs,
	env.Object([
		'gles2/mesh_gles2.cpp',
		'gles2/model_gles2.cpp',
		'gles2/model_loader_gles2.cpp',
		'gles2/label_gles2.cpp',
		'gles2/light_gles2.cpp',
		'gles2/default_shader_gles2.cpp',
		'gles2/texture_gles2.cpp',
		'gles2/texture_loader_gles2.cpp',  # imagemagick dependency
		'gles2/sprite_model_gles2.cpp',
])]

camera_objs = env.Object(['libs/gl/camera.cpp'])

gl_objs = [
	camera_objs,
	env.Object([
		'gl/window.cpp',
		'gl/glfw3_user_input.cpp',
		'gl/glfw3_window.cpp',
])]

md5_objs = [
	env.Object([
		'gles2/animation_gles2.cpp',
		'gl/md5loader.cpp'
])]

touch_objc = [
	env.Object([
		'gles2/touch_joystick_gles2.cpp',
		'gles2/ui/touch_button_gles2.cpp'
])]

env.Program('test/triangle.cpp')
env.Program(['test/test_module.cpp', program_objs])
env.Program(['test/test_program.cpp', program_objs])
env.Program(['test/test_mesh.cpp', gles2_objs])
env.Program(['test/test_camera.cpp', gles2_objs, camera_objs])
env.Program(['test/test_shapes.cpp', gles2_objs, camera_objs])
env.Program(['test/test_window.cpp', gles2_objs, gl_objs])
env.Program(['test/test_texture.cpp', gles2_objs, gl_objs])
env.Program(['test/test_user_input.cpp', gles2_objs, gl_objs])
env.Program(['test/test_glfw3.cpp', gles2_objs, gl_objs])
env.Program(['test/test_free_look.cpp', gles2_objs, gl_objs])
env.Program(['test/bare_model.cpp', gles2_objs, gl_objs])
env.Program(['test/textured_model.cpp', gles2_objs, gl_objs])
env.Program(['test/animated_model.cpp', gles2_objs, gl_objs, md5_objs])

#if __name__ == '__main__':
#	main()
